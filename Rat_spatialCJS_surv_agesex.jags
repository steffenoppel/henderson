

model {


#### PRIORS ####
for(a in 1:2){
for (s in 1:2){
sigma[a,s] ~ dunif(0, 80)     #set up the priors for the time-invariant parameters
sigma2[a,s] <- sigma[a,s]*sigma[a,s]
}
}

for(t in 1:T){
for (u in 1:2){## capture probability differs by traptype
lam0[t,u]~dgamma(0.1,0.1)
}
}

### CONSTANT SURVIVAL - otherwise move dunif statement into T loop
mphi~dunif(0, 1)
#for(t in 1:(T-1)){
#phi[t]~dunif(0, 1)
#}


#### MODEL ####       
   for (i in 1:M){    # loop through the population of M trapped rats
       z[i,first[i]] <- 1       # CJS is conditional on capture, so alive state in first capture period must be 1   
       SX[i,first[i]]~dunif(xl[i], xu[i])    # priors for the activity centers for each individual at first capture
       SY[i,first[i]]~dunif(yl[i], yu[i])    # xl is the lower x coordinate, xu is the upper x value

## OBSERVATION MODEL FOR FIRST SESSION ##
     for(j in 1:ntraps) {     #loop through the J trap locations
           D2[i,j,first[i]] <- pow(SX[i,first[i]]-grid[j,1], 2) + pow(SY[i,first[i]]-grid[j,2],2)## calculate distance from home range center to each trap location
           lam[i,j,first[i]] <- lam0[first[i],ttyp[j,first[i]]]*exp(-D2[i,j,first[i]]/(2*sigma2[ALT[i,1],ALT[i,2]]))## exponential detection function based on distance
           tmp[i,j,first[i]] <- lam[i,j,first[i]]*K[j,first[i]]## correct by number of nights this trap was actually available
           y[i,j,first[i]] ~ dpois(tmp[i,j,first[i]])## count of observations at trap j is a poisson draw of 
}# close the trap loop for first capture period

## OBSERVATION MODEL FOR SUBSEQUENT SESSIONS ##       
for(t in (first[i]+1):T){   #loop through the primary periods after the first capture session
       SX[i,t]~dunif(xl[i], xu[i])    # priors for the activity centers for each individual at first capture
       SY[i,t]~dunif(yl[i], yu[i])    # xl is the lower x coordinate, xu is the upper x value

     for(j in 1:ntraps) {     #loop through the J trap locations
           D2[i,j,t] <- pow(SX[i,t]-grid[j,1], 2) + pow(SY[i,t]-grid[j,2],2)## calculate distance from home range center to each trap location
           lam[i,j,t] <- lam0[t,ttyp[j,t]]*exp(-D2[i,j,t]/(2*sigma2[ALT[i,1],ALT[i,2]]))## exponential detection function based on distance
           tmp[i,j,t] <- lam[i,j,t]*K[j,t]*z[i,t]## correct by number of nights this trap was actually available and by alive state of individual i at time t
           y[i,j,t] ~ dpois(tmp[i,j,t])## count of observations at trap j is a poisson draw of 
}# close the trap loop for subsequent capture periods

### SURVIVAL MODEL FOR TIME PERIOD T ##
phiUP[i,t]<-z[i,t-1]*pow(mphi,1/interval[t-1])      ##phi[t-1]
z[i,t]~dbern(phiUP[i,t])

} ## close the primary session loop

          }## close the individual loop


### DERIVED PARAMETERS FOR SURVIVAL AND CAPTURE PROBABILITY ###

#for(t in 1:(T-1)){
#month.surv[t]<- pow(pow(phi,1/interval[t]),30)
#}
month.surv<- pow(mphi,30)

capt.prob.Sherman<- mean(lam0[1:6,1])
capt.prob.Tomahawk<- mean(lam0[6:7,2])

for(a in 1:2){
for (s in 1:2){
HR_area[a,s]<-(3.141593*(3.36*pow(sigma[a,s],2)))/10000### this is the mean home-range size of rats in ha
}
}


} ## close the model loop


